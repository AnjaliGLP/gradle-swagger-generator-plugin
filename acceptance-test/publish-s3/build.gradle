plugins {
    id 'java'
    id 'maven-publish'
    id 'org.hidetake.swagger.generator'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'jp.classmethod.aws.s3' version '0.30'
}

version = System.getenv('TRAVIS_TAG') ?: 'SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile 'org.springframework.cloud:spring-cloud-starter-feign:1.2.1.RELEASE'
    compile 'org.springframework.cloud:spring-cloud-starter-security:1.1.3.RELEASE'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.11.RELEASE'
    compile 'io.swagger:swagger-annotations:1.5.10'
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.1'
}

generateSwaggerCode {
    language = 'spring'
    inputFile = file('petstore.yaml')
    configFile = file('config.json')
}

compileJava.dependsOn generateSwaggerCode
sourceSets.main.java.srcDir generateSwaggerCode.outputDir

generateSwaggerDoc {
    inputFile = file('petstore.yaml')
}

publishing {
    repositories {
        maven {
            url 's3://gradle-swagger-generator-plugin/maven'
            credentials(AwsCredentials) {
                accessKey System.getenv('AWS_ACCESS_KEY_ID')
                secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.hidetake.gradle.swagger.generator'
            artifactId 'petstore-client'
            version project.version
            from components.java
        }
    }
}

task publishSwaggerDoc(type: jp.classmethod.aws.gradle.s3.SyncTask, dependsOn: generateSwaggerDoc) {
    source generateSwaggerDoc.outputDir
    bucketName 'gradle-swagger-generator-plugin'
    prefix "${project.version}/"
}

publish.dependsOn publishSwaggerDoc
